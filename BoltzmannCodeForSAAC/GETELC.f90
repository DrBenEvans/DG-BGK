      SUBROUTINE GETELC(NELEM ,GEOME ,NGEOM,NNODE,VNPNT,IV,& 
     &                  DISNF,FLUXP ,FLUYP ,UMEAN ,& 
     &                  RHS,DTE ,UX,UY,MU,NFO,RANK,ITIME) 
! 
! *** THIS SUB. COMPUTES THE ELEMENT CONTRIBUTIONS 
! 
      INTEGER NELEM,IE,IN,maxNELEM_PP,NGEOM,NNODE 
      INTEGER VNPNT,IV,RANK,ITIME 
! 
      REAL FLUXP(1,3,NELEM) ,FLUYP(1,3,NELEM) 
      REAL UMEAN(NELEM),DISNF(NNODE,VNPNT,NELEM) 
      REAL RHS(1,3,NELEM),UNKEL(3),FXEL(3),FYEL(3) 
      REAL GEOME(NGEOM,NELEM) 
      REAL DIVE,FXME        ,FYME  
      REAL GEO(6),MU(NNODE,NELEM),NFO(NNODE,NELEM) 
      REAL DTE,UX,UY,C00,EQNF,NF,FREQ 
      REAL AREA 
      REAL C13,ANX,ANY 
      REAL C0,COLL 
! 
      C13=1./3. 
      C0=0.0 
! 
! *** INITIALISE UMEAN 
! 
      CALL RFILLV(UMEAN,NELEM,C0) 
! 
! *** INITIALISE RHS 
! 
      CALL RFILLA(RHS,1,3,NELEM,C0) 
! 
! *** LOOP OVER THE ELEMENTS 
! 
      DO 1000 IE=1,NELEM 
! 
! *** TRANSFER GEOME AND DISNF INTO LOCAL VARIABLES AREA,GEO,UNKEL 
! 
      AREA=0.5*GEOME(7,IE) 
! 
      DO 1001 IN=1,3 
      GEO(IN)=GEOME(IN,IE) 
      GEO(IN+3)=GEOME(IN+3,IE) 
      UNKEL(IN)=DISNF(IN,IV,IE) 
 1001 CONTINUE 
! 
! *** NOW THE DIVERGENCE 
! 
      DIVE=0 
      DO 1005 IN=1,3 
      ANX=GEO(IN) 
      ANY=GEO(IN+3) 
      DIVE=DIVE+ANX*FLUXP(1,IN,IE)+ANY*FLUYP(1,IN,IE) 
 1005 CONTINUE 
!
! *** COMPUTE THE BGK SOURCE TERM FOR EACH ELEMENT
!
        COLL=0.0
        DO 1006 IN=1,3
        EQNF=NFO(IN,IE)
        NF=DISNF(IN,IV,IE)
        FREQ=MU(IN,IE)
        COLL=COLL+FREQ*(EQNF-NF)
 1006 CONTINUE
        COLL=C13*COLL

!    IF((ITIME.EQ.1).OR.(ITIME.EQ.100).OR.(ITIME.EQ.200).OR.(ITIME.EQ.300).OR.(ITIME.EQ.400).OR.(ITIME.EQ.500).OR.(ITIME.EQ.600).OR.(ITIME.EQ.700).OR.(ITIME.EQ.800).OR.(ITIME.EQ.900).OR.(ITIME.EQ.1000))THEN
!      IF((RANK.EQ.17).AND.(IE.EQ.1))THEN
!      WRITE(45,*) UX,UY,COLL
!      ENDIF
!      ENDIF
!
! *** ADD TO THE DIVERGENCE
!
       DIVE=DIVE-COLL    !CAREFUL WITH THE SIGN HERE!!!
! 
! *** MULTIPLY BY 0.5*DELTA-TIME (GLOBAL TIMESTEPPING) 
!	
      DIVE=DIVE*DTE*0.5
! 
! *** STORE IN UMEAN FOR EDGE TREATMENT 
! 
      UMEAN(IE)=DIVE 
! 
! *** GET THE UPDATED UNKEL 
! 
      DO 1020 IN=1,3 
      UNKEL(IN)=UNKEL(IN)-DIVE 
 1020 CONTINUE 
! 
! *** CALCULATE THE ELEMENTS CONTRIBUTION TO THE SECOND STEP 
! *** (THE SECOND TERM ON THE RHS OF EQ. 2.5.1) 
! 
! *** GET THE FLUXES 
! 
      CALL GETFLU(3,UNKEL,FXEL,FYEL,UX,UY) 
! 
! *** GET THE MEAN FLUXES AND ADD BOUNDARY TERMS IF REQUIRED 
! 
      FXME=0 
      FYME=0 
      DO 1040 IN=1,3 
      FXME=FXME+FXEL(IN) 
      FYME=FYME+FYEL(IN) 
 1040 CONTINUE 
! 
      FXME=C13*FXME      !MEAN ELEMENT X-FLUX 
      FYME=C13*FYME      !MEAN ELEMENT Y-FLUX 
! 
! *** NOW COMPUTE THE RHS INCLUDING THE BGK SOURCE TERM FOR EACH DISCONTINUOUS NODE
! 
      DO 1070 IN=1,3
      ANX=GEO(IN)*AREA 
      ANY=GEO(IN+3)*AREA 
      RHS(1,IN,IE)=RHS(1,IN,IE)+ANX*FXME+ANY*FYME 
 1070 CONTINUE
! 
! *** END LOOP OVER THE ELEMENTS 
! 
 1000 CONTINUE
! 
! *** RETURN TO ADVNCE 
! 
      RETURN 
      END 
