SUBROUTINE GETGEO(NELEM, NPOIN, NNODE, NDIMN, NGEOM, INTMA,&
     &                  COORD, GEOME)
!
  IMPLICIT NONE
  INTEGER :: NNODE, NDIMN, NGEOM, NPOIN, NELEM
  REAL COORD(NDIMN, NPOIN)
  REAL GEOME(NGEOM, NELEM)

  INTEGER :: IELEM, IN, INODE
  REAL X(3), Y(3), NXI(3), NET(3)
  REAL :: RNET, RNXI
  REAL RJ1, Y31, X31, Y21, X21, RJ, XIX, XIY, ETX, ETY
!
  INTEGER INTMA(NNODE, NELEM)
! *** INITIAL VALUES GIVEN TO THE VARIABLES NXI AND NET
  DATA NXI/-1.0, 1.0, 0.0/
  DATA NET/-1.0, 0.0, 1.0/
!
! *** THIS SUB EVALUATES N,X & N,Y FOR EACH ELEMENT (LINEAR TRIANGLES)
!     AND THE JACOBIAN (=2*AREA)
!
  DO IELEM = 1, NELEM
!
    X = 0
    Y = 0
    DO INODE = 1, NNODE
      IN = INTMA(INODE, IELEM)
      X(INODE) = COORD(1, IN)
      Y(INODE) = COORD(2, IN)
    ENDDO
!
    X21 = X(2) - X(1)
    X31 = X(3) - X(1)
    Y21 = Y(2) - Y(1)
    Y31 = Y(3) - Y(1)
!
    RJ = X21*Y31 - X31*Y21
    RJ1 = 1./RJ
!
    XIX = Y31*RJ1
    XIY = -X31*RJ1
    ETX = -Y21*RJ1
    ETY = X21*RJ1
!
! *** FORM N,X & N,Y
!
    DO IN = 1, 3
      RNXI = NXI(IN)
      RNET = NET(IN)
      GEOME(IN, IELEM) = XIX*RNXI + ETX*RNET
      GEOME(IN + 3, IELEM) = XIY*RNXI + ETY*RNET
    ENDDO
!
! *** JACOBIAN
!
    GEOME(7, IELEM) = RJ
    IF (RJ .LT. 0000.) PRINT *, ielem
!
  ENDDO
  RETURN
END
